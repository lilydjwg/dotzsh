#!/bin/zsh

# from https://github.com/marlonrichert/zsh-edit; MIT license

emulate -L zsh; setopt extendedglob NO_listbeep NO_shortloops warncreateglobal
local -i move=0

if [[ $WIDGET == *kill-* ]]; then
  zle -f kill
  if (( REGION_ACTIVE )); then
    zle .kill-region
    return
  fi
fi

if [[ $WIDGET == *-shell-word ]]; then
  local w=
  if [[ $WIDGET == *backward* ]]; then
    local -a words=( ${${(z)LBUFFER}:#\;} )
    while [[ -z $w && $#words[@] -gt 0 ]]; do
      w=${(M)LBUFFER%$words[-1]*}
      shift -p words
    done
    move=-$#w
  else
    # We can't split $RBUFFER on words, because that usually doesn't parse correctly.
    local -a words=( ${${(z)BUFFER}:#\;} ) lwords=( ${${(z)LBUFFER}:#\;} )
    words=( $words[$#lwords[@],-1] )
    words[1]=${words[1]#$lwords[-1]}
    while [[ -z $w && $#words[@] -gt 0 ]]; do
      w=${(M)RBUFFER#*$words[1]}
      shift words
    done
    move=+$#w
  fi
else
  local wordchars
  zstyle -s ":edit:$WIDGET:" word-chars wordchars &&
      local +h WORDCHARS="$wordchars"

  local subword='([[:WORD:]]##~*[^[:upper:]]*[[:upper:]]*~*[[:alnum:]]*[^[:alnum:]]*)'
  local word="(${subword}|[^[:WORD:][:space:]]##|[[:space:]]##)"
  if [[ $WIDGET == *backward-* ]]; then
    move=-${(M)#LBUFFER%%${~word}(?|)}
  else
    move=+${(M)#RBUFFER##(?|)${~word}}
  fi
fi

if [[ $WIDGET == *kill-* ]]; then
  # Move the mark instead of the cursor, or else kill-region will add the kill
  # to the wrong end of the cutbuffer.
  (( MARK = CURSOR + move ))
  zle .kill-region
else
  (( CURSOR += move ))
fi

return 0
